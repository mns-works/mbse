---
slug: language-tour
---

# 语言简介

### 包 Package

```
package 'Package Example' {
    import ISO::TorqueValue;
    import ScalarValues::*;

    part def Automobile;

    alias Car for Automobile;
    alias Torque for ISO::TorqueValue;
}
```

*包*的作用是作为一个其成员的命名空间，以及其所拥有成员的一个*容器*。

:::note
成员和拥有成员的区别：拥有成员表示此成员直接在此包内定义，例如上面所定义的`Automobile`，而成员可能包括从其他命名空间下引入的成员。
:::

可以使用*import*来将其他包内的某个成员或者所有成员引入到当前包内。
要引入全部成员，可以使用 _\*_，例如`import ScalarValues::*`。

在包内，可以对成员创建*别名*。别名既可以对包的拥有成员创建，也可以对引入的其他包内的成员创建。

如果包的名称中包含空格，那么需要将其用单引号包裹起来。

#### 成员可见性

```
package 'Package Example' {
    public import ISO::TorqueValue;
    private import ScalarValues::**;

    private part def Automobile;

    public alias Car for Automobile;
    alias Torque for ISO::TorqueValue;
}
```

在包内，可以使用关键字*private*、_public_ 来控制成员的可见性（Visibility）。

可见性关键字可以在*import*前使用。如果使用`public import`，那么相当于将引入的成员再次导出，其相对于引用此包的命名空间，也是可见的。

私有的（_private_）成员对包的外部是不可见的，但是对于包内部的子包而言，则是可见的。

默认情况下，所有成员都是公共的（_public_）。使用 _public_ 关键字只是将此语义显示的标明出来。

### 注释 Comment

```
package 'Comment Example' {
    /* 这是文档注释，是模型的一部分，
     * 默认作为包的说明文档。 */

    comment Comment11 /* 这是一个具名注释 */

    comment about Automobile
    /* 这是一个不具名的文档注释，
     * 使用about来指定说明的元素。
     */

    part def Automobile;

    /**
     * 这是一个文档注释，用来说明紧邻其下的元素。
     *
     */
    alias Car for Automobile;

    // 这是一个批注。
    // 批注以文本形式展示，但是并不是模型的一部分。
    alias Torque for ISO::TorqueValue;

}

```

*注释*用来对模型做出说明，以 _/\*_ 开始，以 _\*/_ 结束。

注释可以作为单独的元素存在，此时其可以具有一个名字。

当注释直接作为其所注释的元素的一部分时，分为几种情况：

1. 元素文档

   以 _/\*\*_ 开始，作为紧邻其下元素的一部分。

2. 不相邻元素文档

   可以使用 `comment about 指定元素` 的方式，将注释和元素放在不同的地方。此时，由于此注释不是单独的元素，所以不能具有名字。

3. 包文档

   只以 _/\*_ 开始，作为其所在包的一部分。

最后，对于模型的单纯批注，可以使用 _//_ 开头。多行批注则可以 _//\*_ 开头。批注不作为模型的一部分。

### 文档 Documentation

```
package 'Documentation Example' {
    doc /* 这是所属包的文档
         */
    part def Automoble {
        doc /* 这是Automobile的文档 */
    }

    alias Car for Automobile;
    alias Torque for ISO::TorqueValue;
}
```

*文档*是一种特殊类型的注释。

### 部件和属性定义

```
part def Vehicle {
    attribute mass : ScalarValues::Real;

    part eng : Engine;

    ref part driver : Person;
}

attribute def VehicleStatus {
    import ScalarValues::*;

    attribute gearSetting : Integer;
    attribute acceleratorPosition : Real;
}

part def Engine;
part def Person;
```

在 SysMLv2 里，广泛使用*定义*（_definition_ ）和*使用*（_usage_）的概念，以支持重用。
*定义*表示某种建模概念的声明，而*使用*则表示定义的概念在某个上下文中的实例。

:::note
*定义*和*使用*都属于命名空间的一种，因此可以在其中使用*import*来引入其他成员。
:::

*部件定义*是系统或者系统组成的类型定义，表示在物理时空上存在并且可变的某种实体。
*部件定义*可以被作为部件或者引用部件来使用。其中，*部件使用*表示一个*聚合特征*，而*引用部件使用*表示一个*引用性*的特征。

*属性定义*表示对修饰性数据的定义，可以用来描述系统或部件。
